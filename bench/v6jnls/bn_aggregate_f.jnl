! bn_aggregate_f.F
! exercise FMRC aggregations

SPAWN "rm -fr aggf"
SPAWN "mkdir aggf"

! *** Create an artificial forecast model run collection ***

! a 20x20 lat-long region
DEFINE AXIS /X=140E:160E:1 xax
DEFINE AXIS /Y=20N:40N:1 yax

! regular mid-monthly axis Jan 1950 to Dec 1960
DEFINE AXIS /UNITS=DAYS /T0=01-JAN-1900 /EDGES /T=01-JAN-1950:31-DEC-1960:`365.2425/12` monthly

! beginning-of-the-month quarter-annual dates Jan 1950 to OCT 1953
LET quarterly_dates = { \
   "01-JAN-1950", "01-APR-1950", "01-JUL-1950", "01-OCT-1950", \
   "01-JAN-1951", "01-APR-1951", "01-JUL-1951", "01-OCT-1951", \
   "01-JAN-1952", "01-APR-1952", "01-JUL-1952", "01-OCT-1952" \
}
LET quarterly_times = DATE1900(quarterly_dates)

! define a predictable value with errors increasing from a given forecast date
! ftaxis is defined with T0 = time-of-forecast
LET thirdpi = `4.0 * ATAN(1.0) / 3.0`
LET final_val = Y[gy=yax] - 0.1 * X[gx=xax]
LET /TITLE="first" fcst = final_val + fcst_error
LET /TITLE="second" fcst_2 = fcst + 2.0

! every three months a two-year forecast in fcst_<n>.nc,
! a three-year forecast in long_fcst_<n>.nc, and a two-year
! single_variable forecast in one_var_fcst_<n>.nc
REPEAT /NAME=fdate /RANGE=1:12 ( \
   LET t0date = quarterly_dates[I=`fdate`] ;\
   LET tzero = quarterly_times[I=`fdate`] ;\
   LET tstart = T[gt=monthly,L=`3 * fdate - 2`] - `tzero` ;\
   LET tend = T[gt=monthly,L=`3 * fdate + 33`] - `tzero` ;\
   DEFINE AXIS /UNIT=DAYS /T0=`t0date` /T=`tstart`:`tend`:`365.2425/12` ftaxis ;\
   LET fcst_error = COS((final_val + `fdate`) * thirdpi) * T[gt=ftaxis] / 100 ;\
   SAVE /CLOBBER /FILE="aggf/fcst_`fdate`.nc" /L=1:24 fcst, fcst_2 ;\
   SAVE /CLOBBER /FILE="aggf/long_fcst_`fdate`.nc" fcst, fcst_2 ;\
   SAVE /CLOBBER /FILE="aggf/one_var_fcst_`fdate`.nc" /L=1:24 fcst \
)

CANCEL VARIABLES/ALL

! ***** End of FMRC file creation ****************

LET files = SPAWN("ls -1 aggf/fcst_*.nc")

! define a complete FMRC aggregation
LIST files  ! notice that they are not properly ordered
FMRC my_fmrc = files
SHOW DATA ! note that member files are hidden
SHOW DATA /MEMBERS

! plot and list the aggregation
GO bn_aggregate_f.sub

CANCEL DATA my_fmrc
SHOW DATA /HIDDEN   ! note that all member files were canceled, too

! agg with 2nd, 3rd, 9th and 10th forecasts missing
USE aggf/fcst_1.nc, aggf/fcst_2.nc, aggf/fcst_3.nc, aggf/fcst_9.nc, aggf/fcst_10.nc, aggf/fcst_11.nc
FMRC /HIDE my_fmrc = XCAT(files[I=7:11],{"6","1"})      ! _4,_5,_6,_7,_8,_11,_1
SHOW DATA /MEMBERS

GO bn_aggregate_f.sub

CANCEL DATA my_fmrc
SHOW DATA /HIDDEN
CANCEL DATA /ALL

! agg with the first forecast of longer time range than the others
FMRC /HIDE my_fmrc = XCAT(files[I=5:11],{"aggf/long_fcst_1.nc"}) ! first member is long
SHOW DATA /MEMBERS
GO bn_aggregate_f.sub
CANCEL DATA my_fmrc

! agg with the sixth forecast of longer time range than the others
FMRC /HIDE my_fmrc = aggf/fcst_1.nc, aggf/fcst_2.nc, aggf/fcst_3.nc, aggf/fcst_4.nc, \
                     aggf/fcst_5.nc, aggf/long_fcst_6.nc, aggf/fcst_7.nc, aggf/fcst_8.nc
SHOW DATA /MEMBERS
GO bn_aggregate_f.sub
CANCEL DATA my_fmrc

! dataset name pulled implicitly from the script name
FMRC /HIDE aggf/fcst_7.nc, aggf/fcst_4.nc, aggf/fcst_5.nc, aggf/fcst_6.nc, aggf/fcst_3.nc
SHOW DATA /MEMBERS
CANCEL DATA /ALL

! agg where one dataset lacks one of the variables
LET onevarlist = { "aggf/fcst_7.nc", "aggf/fcst_4.nc", "aggf/fcst_5.nc", "aggf/one_var_fcst_6.nc" }
FMRC /HIDE my_fmrc = onevarlist
SHOW DATA /MEMBERS
STAT fcst

! TODO: jointly using one dataset no longer possible, but should be hidden
! so the same data file can be used again under a different ID
!
! cancel a component datafile used to two aggregations
! cancel both dataset instances, and thus both aggregations - or can differentiate?
! FMRC /HIDE other_fmrc = onevarlist
! CANCEL DATA aggf/fcst_4.nc
! SHOW DATA

CANCEL DATA /ALL

! TODO: jointly using one dataset no longer possible, but should be hidden
! so the same data file can be used again under a different ID
!
! cancel one of two aggregations using the same datafiles
! cancel only the one aggregation and its datasets, leaving the other
! FMRC /HIDE my_fmrc = onevarlist
! FMRC /HIDE other_fmrc = onevarlist
! CANCEL DATA my_fmrc
! SHOW DATA
! CANCEL DATA other_fmrc
! SHOW DATA

! TODO: the following crashes
! agg where a LET/D variable is used to fill in a missing variable in the first dataset
! USE aggf/one_var_fcst_1.nc
! LET/D=one_var_fcst_1.nc fcst_2 = fcst + 2  ! compute same value as file vars
! SHOW DATA
! FMRC /HIDE my_fmrc = aggf/one_var_fcst_1.nc, aggf/fcst_2.nc, aggf/fcst_3.nc, \
!                      aggf/fcst_4.nc, aggf/fcst_5.nc, aggf/fcst_6.nc
! SHOW DATA /MEMBERS
! GO bn_aggregate_f.sub
! CANCEL DATA /ALL

! agg where a LET/D variable is used to fill in a missing variable in a middle dataset
USE aggf/one_var_fcst_5.nc
LET/D=one_var_fcst_5.nc fcst_2 = fcst + 2  ! compute same value as file vars
SHOW DATA
FMRC/HIDE my_fmrc = aggf/fcst_1.nc, aggf/fcst_2.nc, aggf/fcst_3.nc, \
                    aggf/fcst_4.nc, aggf/one_var_fcst_5.nc, aggf/fcst_6.nc
SHOW DATA /MEMBERS
GO bn_aggregate_f.sub
CANCEL DATA /ALL

! *************************************
! deliberate errors
SET MODE IGNORE

! TODO: the following crashes
! error - duplicate file
! FMRC my_fmrc = XCAT(files, "aggf/fcst_2.nc")
! SHOW DATA /HIDDEN

! error - unknown dataset
FMRC my_fmrc = XCAT(files, "aggf/no_exist.nc")
SHOW DATA /HIDDEN

SET MODE /LAST IGNORE
